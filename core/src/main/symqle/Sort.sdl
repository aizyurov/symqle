#############################################################################
#
#   Copyright 2010-2013 Alexander Izyurov
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.package org.symqle.common;
#
##############################################################################

# OrderByClause ::= ORDER BY SortSpecificationList
# SortSpecificationList ::= SortSpecification [ { comma zSortSpecification }... ]
# SortSpecification ::= SortKey [ ordering_specification ] [ null_ordering ]
# SortKey ::= ValueExpression
# ordering_specification ::= ASC | DESC
# null_ordering ::= NULLS FIRST | NULLS LAST
# 
# rephrased in trivial manner - see below
# 

public interface OrderByClause : SqlBuilder {}

OrderByClause ::= ORDER BY ssl:SortSpecificationList :: private OrderByClause createOrderByClause
;

public interface SortSpecificationList : SqlBuilder {}

#
SortSpecificationList ::=
    ss:SortSpecification :: private SortSpecificationList createSortSpecificationList
|
    ssl:SortSpecificationList COMMA ss:SortSpecification :: private SortSpecificationList appendSortSpecification
;

public interface SortSpecification : SqlBuilder {}

public class AbstractSortSpecification implements SortSpecification {}

SortSpecification ::=
    sos:SortOrderingSpecification :: implicit
|
    /**
     * Applies {@code NULLS FIRST} to {@code this}.
     * @return resulting sort specification
     */
    sos:SortOrderingSpecification NULLS FIRST :: AbstractSortSpecification nullsFirst {}
|
    /**
     * Applies {@code NULLS LAST} to {@code this}.
     * @return resulting sort specification
     */
    sos:SortOrderingSpecification NULLS LAST :: AbstractSortSpecification nullsLast {}
;

public interface SortOrderingSpecification : SqlBuilder {}

public class AbstractSortOrderingSpecification implements SortOrderingSpecification {}

public interface SortKey : SqlBuilder {}

SortKey ::= ve:ValueExpression<?> :: implicit
;

SortOrderingSpecification ::=
    key:SortKey :: implicit
|
    /**
     * Applies {@code ASC} to {@code this}.
     * @return resulting sort ordering specification with ascending order
     */
    key:SortKey ASC :: AbstractSortOrderingSpecification asc {
        public SqlBuilder z$sqlOfSortOrderingSpecification(final SqlContext context) {
             return context.get(Dialect.class).SortOrderingSpecification_is_SortKey_ASC(key.z$sqlOfSortKey(context));
        }
    }
|
    /**
     * Applies {@code DESC} to {@code this}.
     * @return resulting sort ordering specification with descending order
     */
    key:SortKey DESC :: AbstractSortOrderingSpecification desc {
        /**
        * Creates an SqlBuilder representing SortOrderingSpecification
        * @param context the Sql construction context
        * @return constructed Sql
        */
        public SqlBuilder z$sqlOfSortOrderingSpecification(final SqlContext context) {
             return context.get(Dialect.class).SortOrderingSpecification_is_SortKey_DESC(key.z$sqlOfSortKey(context));
        }
     }
;
